plugins {
    id 'maven'
    id 'war'
}

repositories {
    jcenter()
    mavenLocal()

    // Used for web-test browser testing.
    maven {
        url = 'http://www.cadc-ccda.hia-iha.nrc-cnrc.gc.ca/m2repo'
    }
}

dependencies {
    compile 'log4j:log4j:1.2.+'

    compile 'org.opencadc:caom2-search-server:[1.2.6, )'

    providedCompile 'javax.servlet:javax.servlet-api:3.1.+'
    providedCompile 'javax.servlet:javax.servlet-api:3.0.+'

    runtime 'commons-logging:commons-logging:1.+'
    runtime 'org.postgresql:postgresql:42.0.0.jre7'
    runtime 'jstl:jstl:1.+'

    testCompile 'junit:junit:4.+'
    testCompile 'org.skyscreamer:jsonassert:1.+'
    testCompile 'org.easymock:easymock:3.+'
}

ext {
    // Change this value to test other locations, such as Production or RC.
    intTest_web_app_url = 'http://localhost:8080/search/'
}

sourceCompatibility = 1.7

group = 'org.opencadc'

version = '1002'

// Modify this to your liking!
def docker_image_name = 'opencadc/archive-search:bootstrap-3.3.7'

war {
    archiveName = 'search##' + project.version + '.war'
}

clean {
    doFirst {
        exec {
            executable 'rm'
            args('-rf', './docker')
        }
    }
}

task initDockerize(type: Copy, dependsOn: [build]) {
    from 'Dockerfile'
    from 'org.opencadc.search.properties'
    into 'docker/'
}

task copyWAR(type: Copy, dependsOn: war) {
    from 'build/libs/'
    into 'docker/'
    include '*.war'
}

task dockerize(type: Exec, dependsOn: [initDockerize, copyWAR]) {
    commandLine 'docker', 'build', '-t', docker_image_name, '-f', './docker/Dockerfile', './docker/'
}

sourceSets {
    intTest
}

['firefox', 'chrome'].each { driver ->
    task "intTest${driver.capitalize()}"(type: Test) { driverTest ->
        testClassesDir = sourceSets.intTest.output.classesDir
        classpath = sourceSets.intTest.runtimeClasspath
        reports {
            html.destination = reporting.file("$name/html")
        }
        dependencies {
            intTestCompile 'junit:junit:4.+'
            intTestCompile 'org.opencadc:cadc-web-test:1.+'
            intTestCompile 'org.seleniumhq.selenium:selenium-java:3.+'
        }

        systemProperty "driver", "${driver}"

        if (project.hasProperty('intTest_selenium_server_url')) {
            systemProperty 'selenium.server.url', project.intTest_selenium_server_url
        }
        else {
            systemProperty 'selenium.server.url', 'http://cadcint2.cadc.dao.nrc.ca:4444'
        }

        if (project.hasProperty('intTest_web_app_url')) {
            systemProperty 'web.app.url', project.intTest_web_app_url
        }

        maxParallelForks = 3
    }
}
