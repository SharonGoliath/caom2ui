plugins {
    id 'maven'
    id 'war'
}

repositories {
    jcenter()
    mavenLocal()

    // repo for jsky
    maven {
        url = 'http://java.freehep.org/maven2'
    }

    // Used for web-test browser testing.
    maven {
        url = 'http://www.cadc-ccda.hia-iha.nrc-cnrc.gc.ca/m2repo'
    }
}

dependencies {
    compile 'log4j:log4j:1.2.+'
    compile 'com.opencsv:opencsv:3.+'
    compile 'org.jdom:jdom2:2.+'
    compile 'org.json:json:20160810'
    compile 'org.opencadc:cadc-access-control:1.+'
    compile 'org.opencadc:cadc-access-control-identity:1.+'
    compile 'org.opencadc:cadc-uws:1.+'
    compile 'org.opencadc:cadc-uws-server:1.+'
    compile 'org.opencadc:cadc-tap-server:1.+'
    compile 'org.opencadc:cadc-adql:1.+'
    compile 'org.opencadc:cadc-util:1.+'
    compile 'org.opencadc:cadc-dali:1.+'
    compile 'org.opencadc:caom2:2.+'
    compile 'org.opencadc:cadc-registry:1.+'
    compile 'org.opencadc:cadc-vosi:1.+'
    compile 'org.opencadc:search-server-lib:1.+'
    compile 'org.apache.httpcomponents:httpclient:4.+'
    compile 'org.apache.commons:commons-configuration2:2.+'
    compile 'commons-net:commons-net:3.+'

    providedCompile 'javax.servlet:javax.servlet-api:3.1.+'
    providedCompile 'javax.servlet:javax.servlet-api:3.0.+'

    runtime 'commons-logging:commons-logging:1.+'
    runtime 'org.postgresql:postgresql:9.4.1209.jre7'
    runtime 'jstl:jstl:1.+'

    testCompile 'junit:junit:4.+'
    testCompile 'org.skyscreamer:jsonassert:1.+'
    testCompile 'org.easymock:easymock:3.+'
}

ext {
    // Change this value to test other locations, such as Production or RC.
    intTest_web_app_url = 'http://localhost:8080/search/'
}

sourceCompatibility = 1.7

group = 'org.opencadc'

version = '1001'

// Modify this to your liking!
def docker_image_name = 'opencadc/search'

war {
    archiveName = 'search##' + project.version + '.war'
}

clean {
    doFirst {
        exec {
            executable 'rm'
            args('-rf', './docker')
        }
    }
}

task copySQL(type: Copy) {
    from 'sql/'
    include '*.sql'
    into 'docker/sql/'
}

task initDockerize(type: Copy, dependsOn: [build, copySQL]) {
    from 'Dockerfile'
    from 'org.opencadc.search.properties'
    into 'docker/'
}

task copyWAR(type: Copy, dependsOn: war) {
    from 'build/libs/'
    into 'docker/'
    include '*.war'
}

task dockerize(type: Exec, dependsOn: [initDockerize, copyWAR]) {
    commandLine 'docker', 'build', '-t', docker_image_name, '-f', './docker/Dockerfile', './docker/'
}

sourceSets {
    intTest
}

['firefox', 'chrome'].each { driver ->
    task "intTest${driver.capitalize()}"(type: Test) { driverTest ->
        testClassesDir = sourceSets.intTest.output.classesDir
        classpath = sourceSets.intTest.runtimeClasspath
        reports {
            html.destination = reporting.file("$name/html")
        }
        dependencies {
            intTestCompile 'junit:junit:4.+'
            intTestCompile 'ca.nrc.cadc:web-test:1.+'
            intTestCompile 'org.seleniumhq.selenium:selenium-java:2.53.1'
        }

        systemProperty "driver", "${driver}"

        if (project.hasProperty('intTest_selenium_server_url')) {
            systemProperty 'selenium.server.url', project.intTest_selenium_server_url
        }
        else {
            systemProperty 'selenium.server.url', 'http://cadcint2.cadc.dao.nrc.ca:4444'
        }

        if (project.hasProperty('intTest_web_app_url')) {
            systemProperty 'web.app.url', project.intTest_web_app_url
        }

        maxParallelForks = 3
    }
}